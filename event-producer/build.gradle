plugins {
	id 'org.springframework.boot' 
	id 'io.spring.dependency-management'
	id 'com.palantir.docker' 
	id 'com.palantir.git-version' 
	id 'java'
	id 'project-report'
}

springBoot {
	buildInfo()
}

def versionDetails = versionDetails()
def branchName = versionDetails.branchName
if (System.getenv("CI_COMMIT_REF_NAME") != null) {
	branchName = System.getenv("CI_COMMIT_REF_NAME")
}

if (branchName == "master") {
	version = gitVersion()
} else {
	version = "${branchName}-${gitVersion()}"
}
sourceCompatibility = 8
targetCompatibility = 8

docker.dependsOn bootJar


docker {
	name "registry.gitlab.com/dajoni/spring-playground/event-producer:${project.version}"
	tags 'latest'
	files bootJar.outputs
	dockerfile file("src/main/docker/Dockerfile")
}

test {
	useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
	compileOnly("org.projectlombok:lombok:1.16.20")
	// gradle 4.9 and 1.16.22 are incompatible
	compileOnly("org.springframework.boot:spring-boot-configuration-processor")
    implementation project(":avro-schema")
	implementation('org.springframework.boot:spring-boot-starter')
	implementation("io.confluent:kafka-avro-serializer:4.1.1")
    implementation("org.apache.kafka:kafka_2.11:1.1.1-cp1")
    implementation('log4j:log4j:1.2.17')

    implementation('org.springframework.kafka:spring-kafka') {
        // using clients from Confluent
		exclude module: "kafka-clients"
    }



	testCompileOnly("org.projectlombok:lombok:1.16.20")
	// gradle 4.9 and 1.16.22 are incompatible
	testImplementation('org.junit.jupiter:junit-jupiter-api:5.2.0')
	testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.0')
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
        // using junit 5
		exclude group: "junit", module: "junit"
	}
    testImplementation('org.springframework.kafka:spring-kafka-test') {
        // using clients from Confluent
		exclude module: "kafka-clients"
	}
    testImplementation("org.apache.kafka:kafka-clients:1.1.1-cp1:test")
    testImplementation('log4j:log4j:1.2.17')

    configurations.all {
        // using slf4j from spring
        exclude group: "log4j", module: "log4j"
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }
}
